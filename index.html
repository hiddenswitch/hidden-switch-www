<html>
	<head>
		<title>Conway’s Game of Life</title>
        <meta http-equiv="Content-Type" content="charset=utf-8">
        <meta property="og:title" content="Conway’s Game of Life" />
		<meta property="og:type" content="website" />
		<meta property="og:description" content="The classic Conway's Game of Life implemented in JavaScript and hardware-accelerated WebGL." />
		<meta property="og:url" content="http://leyanlo.github.com/life/" />
		<meta property="og:image" content="http://leyanlo.github.com/life/preview.jpg" />
		<meta property="og:site_name" content="Conway’s Game of Life" />
		<meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, user-scalable=no" />
		<link rel="image_src" href="http://leyanlo.github.com/life/preview.jpg" />
		<link rel="stylesheet" type="text/css" href="main.css" />
		<script src="jquery-1.8.2.min.js"></script>
		<script src="jquery.mousewheel.js"></script>
		<script src="underscore-min-1.4.2.js"></script>
		<script src="gl-matrix-min.js"></script>
		<script src="util.js"></script>
		<script src="3d.js"></script>
		<script src="main.js"></script>
		<!-- shaders -->
		<script id="cell-random-noise-fs" type="x-shader/x-fragment">
			#version 100
		    precision mediump float;
		    varying vec2 fragmentPosition;
		    uniform vec2 seed;
		    float rand (vec2 co){
    			return fract (sin (dot (co.xy ,vec2 (12.9898,78.233))) * 43758.5453);
			}
		    void main (void) {
		    	float noise = rand (fragmentPosition + seed) > 0.6 ? 1.0 : 0.0;
		        gl_FragColor = vec4 (noise, 1, 0, 1);
		    }
		</script>
		<script id="cell-iteration-fs" type="x-shader/x-fragment">
			#version 100
		    precision highp float;
		    varying vec2 texCoord;
		    uniform sampler2D previousStep;
		    uniform vec2 screenSpace;
		    uniform sampler2D rules;
		    vec4 pixelAt (vec2 offset) {
		    	return texture2D (previousStep, texCoord + offset * screenSpace);
		    }
		    void main (void) {
		    	vec4 cell = pixelAt (vec2 ( 0,  0));
		    	float neighbours =
		    	    (pixelAt (vec2 (-1.0, -1.0)) +
		        	 pixelAt (vec2 ( 0.0, -1.0)) +
		        	 pixelAt (vec2 ( 1.0, -1.0)) +
		        	 pixelAt (vec2 ( 1.0,  0.0)) +
		        	 pixelAt (vec2 ( 1.0,  1.0)) +
		        	 pixelAt (vec2 ( 0.0,  1.0)) +
		        	 pixelAt (vec2 (-1.0,  1.0)) +
		        	 pixelAt (vec2 (-1.0,  0.0))).x;
		    	vec4 decision = texture2D (rules, vec2 (neighbours / 16.0, 0.0));
		    	if (cell.x < 0.9 && decision.g > 0.0) {
		    		gl_FragColor = vec4 (1.0, 1.0, 0.0, 1.0);
		    	} else if (cell.x > 0.0 && decision.r > 0.0) {
		    		gl_FragColor = vec4 (0.0, 1.0, cell.z, 1.0);
		    	} else {
		    		gl_FragColor = cell * vec4 (1.0, 0.994, 1.0, 1.0);
		    	}
		    }
		</script>
		<script id="cell-brush-fs" type="x-shader/x-fragment">
			#version 100
		    precision highp float;
		    varying vec2 fragmentPosition;
		    varying vec2 texCoord;
		    uniform sampler2D cells;
		    uniform sampler2D rules;
		    uniform vec2 brushPosition1;
		    uniform vec2 brushPosition2;
		    uniform float brushSize;
		    uniform mat4 pixelSpace;
		    uniform vec2 screenSpace;
		    uniform vec2 seed;
		    uniform bool noise;
		    uniform bool animate;
		    uniform float fill;
		    float rand (vec2 co) {
    			return fract (sin (dot (co.xy ,vec2 (12.9898,78.233))) * 43758.5453) > 0.6 ? 1.0 : 0.0;
			}
		    float dotdot (vec2 x) { return dot (x, x); }
		    float distToSegmentSquared (vec2 v, vec2 w, vec2 p) {
				float l2 = dotdot (v - w);
				if (l2 == 0.0) return dotdot (p - v);
				float t = dot(p - v, w - v) / l2;
				if (t < 0.0) return dotdot (p - v);
				else if (t > 1.0) return dotdot (p - w);
				vec2 projection = v + t * (w - v);
				return dotdot (p - projection);
			}
			vec4 pixelAt (vec2 offset) {
		    	return texture2D (cells, texCoord + offset * screenSpace);
		    }
		    void main (void) {
		    	if (distToSegmentSquared (
		    		(vec4(brushPosition1,1,1)*pixelSpace).xy,
		    		(vec4(brushPosition2,1,1)*pixelSpace).xy,
		    		(vec4(fragmentPosition,1,1)*pixelSpace).xy) < brushSize * brushSize) {
		    		gl_FragColor = vec4 (fill * (noise ? rand (seed + fragmentPosition) : 1.0), 0.98, 1.0, 1.0);
		    	} else {
		    		vec4 cell = pixelAt (vec2 ( 0,  0));
		    		if (animate) {
				    	float neighbours =
				    		(pixelAt (vec2 (-1.0, -1.0)) +
				        	 pixelAt (vec2 ( 0.0, -1.0)) +
				        	 pixelAt (vec2 ( 1.0, -1.0)) +
				        	 pixelAt (vec2 ( 1.0,  0.0)) +
				        	 pixelAt (vec2 ( 1.0,  1.0)) +
				        	 pixelAt (vec2 ( 0.0,  1.0)) +
				        	 pixelAt (vec2 (-1.0,  1.0)) +
				        	 pixelAt (vec2 (-1.0,  0.0))).x;
				    	vec4 decision = texture2D (rules, vec2 (neighbours / 16.0, 0.0));
				    	if (cell.x < 0.9 && decision.g > 0.0) {
				    		cell = vec4 (1.0, 1.0, 0.0, 1.0);
				    	} else if (cell.x > 0.0 && decision.r > 0.0) {
				    		cell = vec4 (0.0, 1.0, cell.z, 1.0);
				    	} else {
				    		cell = cell * vec4 (1.0, 0.994, 1.0, 1.0);
				    	}
			    	}
			    	gl_FragColor = cell;
		    	}
		    }
		</script>
		<script id="cell-vs-pixeloffset" type="x-shader/x-vertex">
			#version 100
			precision highp float;
		    attribute vec3 position;
		    uniform vec2 pixelOffset;
		    varying vec2 fragmentPosition;
		    varying vec2 texCoord;
		    void main (void) {
		    	fragmentPosition = position.xy;
		    	texCoord = (position.xy + 1.0) * 0.5;
		        gl_Position = vec4 (position.xy + pixelOffset, 0.0, 1.0);
		    }
		</script>
		<script id="cell-vs" type="x-shader/x-vertex">
			#version 100
			precision mediump float;
		    attribute vec3 position;
		    varying vec2 fragmentPosition;
		    varying vec2 texCoord;
		    void main (void) {
		    	fragmentPosition = position.xy;
		    	texCoord = (position.xy + 1.0) * 0.5;
		        gl_Position = vec4 (position.xy, 0.0, 1.0);
		    }
		</script>
		<script id="draw-cells-fs" type="x-shader/x-fragment">
			#version 100
		    precision mediump float;
		    varying vec2 texCoord;
		    uniform sampler2D cells;
		    void main (void) {
		    	vec4 cell = texture2D (cells, texCoord);
		    	if (cell.x < 0.5) {
		    		if (cell.z > 0.5) {
		    			gl_FragColor = vec4 (0.0, 0.0, 0.0, 1.0);
		    		} else {
		    			gl_FragColor = vec4 (0.0, cell.y, cell.z, 1.0);
		    		}
		    	} else {
		    		gl_FragColor = vec4 (cell.x, cell.y, 0.0, 1.0);
		    	}
		    }
		</script>
		<script id="simple-vs" type="x-shader/x-vertex">
			#version 100
			precision mediump float;
		    attribute vec3 position;
		 	varying vec2 texCoord;
		 	uniform mat4 transform;
		    void main (void) {
		    	texCoord = ((position + 1.0) * 0.5).xy;
		        gl_Position = transform * vec4 (position, 1.0);
		    }
		</script>
	</head>
	<body>
		<div class="viewport-container">
			<canvas class="viewport" />
		</div>
	</body>
</html>
